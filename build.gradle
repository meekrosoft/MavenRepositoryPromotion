apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'application'

def promoteToEnv = 'test'
String nexusUrl = 'http://35.158.242.34:8081'
String targetGroup = 'net.praqma.codeacademy'
String targetArtifact = 'gildedrose'
String targetVersion = '0.0.2-2017.09.26T12.23.20.258-9fc13ae88875e4dcf62fc55e0b42ece5ef7bbe69'
String targetBinary = "${targetGroup}:${targetArtifact}:${targetVersion}"
String targetDir = 'target'

def repoList = [
    dev: [
        from: 'N/A',
        target: 'repository/workshop-dev/'
    ],
    test: [
        from: 'repository/workshop-dev/',
        target: 'repository/workshop-test/'
    ],
    staging: [
        from: 'repository/workshop-test/',
        target: 'repository/workshop-staging/'
    ],
    prod: [
        from: 'repository/workshop-staging/',
        target: 'repository/workshop-prod/'
    ]
]

repositories {
    maven {
        url "${nexusUrl}/${repoList.get(promoteToEnv).from}"
    }
}


dependencies{
    compile targetBinary
}

task copyDependencies(type: Copy) {
   from configurations.compile
   into targetDir
}

def someFile = file("${targetDir}/${targetArtifact}-${targetVersion}.jar")
artifacts {
    archives someFile
}


publishing {
    repositories {
        maven {
            url "${nexusUrl}/${repoList.get(promoteToEnv).target}"
            credentials {
                username "$mavenUser"
                password "$mavenPassword"
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId targetGroup
            artifactId targetArtifact
            version targetVersion

            artifact(someFile) {
                builtBy copyDependencies
            }
        }
    }
}
